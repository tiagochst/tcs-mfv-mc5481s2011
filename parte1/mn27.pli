# K-COLORING PROBLEM
#
# This problem finds a least number of colors 
# necessaty to paint a map
#
# Authors:
#   Murilo Fossa Vicentini
#   Tiago Chedraoui Silva

param n;
/*number of vertex*/

set V:= 1..n; 
/* n vertex */

set E within {i in V, j in V: i<j}; 
/* edges of undirect graph */

set C:= 1..n;
/* n avaiable colors */

var x{u in V,k in C}, binary;
/* vertex colors */

var y{k in C}, binary;
/* used colors */

s.t. vertex{u in V}:sum{k in C} x[u,k]=1;
/* Each vertex must have only one color */

s.t. vertex_color{u in V,k in C}: x[u,k]<=y[k];
/* If a color is used, it values must be 1*/

s.t. edge{(u,v) in E,k in C}: x[u,k]+x[v,k]<=1;
/* If a color is used, it values must be 1*/

minimize num_color: sum{k in C } y[k];
/* number of colors used*/

data;

param n:=3; /*three vertex*/
set E:=(1,2) (1,3) ; /* all neighbours*/

end;